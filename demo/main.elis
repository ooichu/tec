(load "config.elis")
(load "utils.elis")
(load "actor.elis")
(load "player.elis")
(load "enemy.elis")
(load "monster.elis")
(load "ufo.elis")
(load "ghost.elis")
(load "particle.elis")
(load "pufft.elis")
(load "pickup.elis")
(load "gem.elis")

(= init (func ()
  (play music.wav t)  
  (= level  (tilemap "maps/level.dat")
     clouds (tilemap "maps/clouds.dat")
     (enemies pickups player particles) nil
  )
  ; spawn actors
  (let spawn (tilemap "maps/spawn.dat"))
  (for y 0 (< y (height spawn)) (= y (+ y 1))
    (for x 0 (< x (width spawn)) (= x (+ x 1))
      (let id  (peek x y spawn)
           pos (cons
             (* x (width tiles.bmp))
             (* y (width tiles.bmp))
           )
      )
      (if (is id 39) (Player  pos)
          (is id 40) (Monster pos)
          (is id 41) (Ufo     pos)
          (is id 42) (Ghost   pos)
          (is id 43) (Gem     pos)
      )
    )
  )
))

(= solid? (func (x y)
  (let t (peek (// x 8) (// y 8) level))
  (or (and (< -1 t) (< t 15)) (< x 0))
))

(= draw-map (func (px py map)
  (let c (camera '(0 . 0)))
  (draw (* px (car c)) (* py (cdr c)) tiles.bmp map)
  (camera c)
))

(= group-step (func (group)
  (let res nil)
  (foreach e (eval group)
    (e':step)
    (when (e':alive?)
      (push res e)
    )
  )
  (set group res)
))

(= message (func (msg)
  (let w (+ 2 (* (width font.bmp) (strlen msg)))
       h (* (width font.bmp) 3)
       x (/ (- (width ) w) 2)
       y (/ (- (height) h) 2)
       c (camera '(0 . 0))
  )
  (fill 15 x y w h)
  (fill 0 (+ x 1) (+ y 1) (- w 2) (- h 2))
  (draw (+ x 1) (+ y (width font.bmp)) font.bmp msg)
  (camera c)
))

(= step (func ()
  (clear 13)
  ; draw background
  (draw-map 0.80 0.80 clouds)
  (draw-map 1.00 1.00 level)
  ; draw foreground
  (group-step 'enemies)
  (group-step 'pickups)
  (if player
      (do
        (player':step)
        (unless pickups (message "You won!"))
      )
      (do
        (message "You lose! Press space")
        (when (key "space") (init))
      ) 
  )
  (group-step 'particles)
))
